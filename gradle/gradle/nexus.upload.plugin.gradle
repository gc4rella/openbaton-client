gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it  instanceof Sign }) {
        Console console = System.console()
        console.printf "\n\nWe have to sign some things in this build." +
                "\n\nPlease enter your signing details.\n\n"

        def id = console.readLine("PGP Key Id: ")
        def file = console.readLine("PGP Secret Key Ring File (absolute path): ")
        def password = console.readPassword("PGP Private Key Password: ")

        allprojects { ext."signing.keyId" = id }
        allprojects { ext."signing.secretKeyRingFile" = file }
        allprojects { ext."signing.password" = password }


        console.printf "\nThanks.\n\n"
    }
}

subprojects {
    apply plugin: 'com.bmuschko.nexus'

    modifyPom {
        project {
            name 'Open Baton SDK'
            description 'Openbaton libraries for integrating with the NFV Orchestrator NBI'
            url 'http://openbaton.github.io'
            inceptionYear '2015'
            scm {
          	connection 'scm:git:git@github.com:openbaton/openbaton-client.git'
          	developerConnection 'scm:git:git@github.com:openbaton/openbaton-client.git'
          	url 'https://github.com/openbaton/openbaton-client'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repositories'
                }
            }

            developers {
                developer {
                    id 'lto'
                    name 'Lorenzo Tomasini'
                    email 'lorenzo.tomasini@fokus.fraunhofer.de'
                }
                developer {
                    id 'gca'
                    name 'Giuseppe Carella'
                    email 'giuseppe.carella@fokus.fraunhofer.de'
                }
            }
        }
    }

    extraArchive {
        sources = true
        tests = false
        javadoc = true
    }

    nexus {
        sign = true
        repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
        snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}
